import com.github.benmanes.gradle.versions.updates.DependencyUpdatesTask
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    kotlin("jvm") version "2.1.20"
    id("org.jlleitschuh.gradle.ktlint") version "12.2.0"
    id("com.gradleup.shadow") version "8.3.6"
    id("com.github.ben-manes.versions") version "0.52.0"
    application
}

group = "eu.iamgio.quarkdown"
version = "1.0-SNAPSHOT"

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply(plugin = "org.jlleitschuh.gradle.ktlint")
}

dependencies {
    subprojects.forEach {
        implementation(it)
    }
}

application {
    mainClass.set("eu.iamgio.quarkdown.cli.QuarkdownCliKt")
}

tasks.build {
    dependsOn("shadowJar")
}

tasks.jar {
    enabled = false // The jar is generated by shadowJar
}

tasks.test {
    useJUnitPlatform()
    dependsOn(tasks.ktlintCheck)
}

tasks.distZip {
    archiveVersion.set("")

    // The module 'libs' contains .qmd library files that are saved in the lib/qmd directory of the distribution zip.
    val librariesModule = project(":quarkdown-libs")

    into("${archiveBaseName.get()}/lib/qmd") {
        from(librariesModule.file("src/main/resources")) {
            include("*.qmd")
        }
    }
}

tasks.wrapper {
    gradleVersion = "8.3"
    distributionType = Wrapper.DistributionType.ALL
}

tasks.withType<KotlinCompile> {
    compilerOptions {
        jvmTarget.set(JvmTarget.JVM_17)
    }
}

tasks.withType<ShadowJar> {
    archiveVersion.set("")
    archiveClassifier.set("")
}

tasks.withType<DependencyUpdatesTask> {
    rejectVersionIf {
        sequenceOf("alpha", "beta", "preview", "-m", "-rc").any {
            candidate.version.contains(it, ignoreCase = true)
        }
    }
}
